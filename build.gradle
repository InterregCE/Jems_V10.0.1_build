plugins {
    id "cloudflight-gradle-plugin" version "6.3.0"
}

// add version-postfix if build was triggered by customer
// no postfix if built within teamcity or with system property flag set to REMOVE
def customerPostfix = ""
if (!(System.getenv("TEAMCITY_BUILD") != null && System.getenv("TEAMCITY_BUILD").toBoolean())
        && (!System.getProperty("CUSTOMER_VERSION_POSTFIX") || System.getProperty("CUSTOMER_VERSION_POSTFIX") != "REMOVE")) {
    customerPostfix = System.getProperty("CUSTOMER_VERSION_POSTFIX") ? "-" + System.getProperty("CUSTOMER_VERSION_POSTFIX") : "-P"
}

description "Jems Project"
group "io.cloudflight.jems"
version "5.0.0$customerPostfix"

cloudflight {
    softwareProjectKey = "interact/ems"
}

subprojects {
    if (isKotlinProject(it)) {

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            }
        }

        dependencies {
            implementation platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
            annotationProcessor platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
            testImplementation platform("io.cloudflight.platform:platform-test-bom:$cloudflightPlatformVersion")
            kapt platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")

            constraints {
                implementation "com.github.doyaaaaaken:kotlin-csv-jvm:0.15.0"
                implementation "org.mapstruct:mapstruct:1.4.2.Final"
                kapt 'org.mapstruct:mapstruct-processor:1.4.2.Final'
            }
        }
    }
    if (isServerProject(it) && (isCloudflightBuild(it) || System.getProperty("CUSTOMER_VERSION_POSTFIX"))) {
        bootJar {
            manifest {
                attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
            }
        }
    }
}

// prevent local compile error
apply plugin: "org.sonarqube"
// add rules for sonarqube
sonarqube {
    properties {
        property "sonar.exclusions", "**/*.spec.ts"
        // exclude frontend files from code coverage
        property "sonar.coverage.exclusions", "**/*.ts, **/*.html, **/*.scss, **/*.properties"
        // exclude certain big enums from code duplication checks
        property "sonar.cpd.exclusions", "**/NaceGroupLevel.kt, **/NaceGroupLevelDTO.kt"

        property "sonar.issue.ignore.multicriteria", "e1,e2,e3"
        // ignore rule: readonly was decided to provide no big advantage
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "typescript:S2933"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "**/*.ts"
        // ignore rule: not afraid of long ts lines
        property "sonar.issue.ignore.multicriteria.e2.ruleKey", "typescript:S103"
        property "sonar.issue.ignore.multicriteria.e2.resourceKey", "**/*.ts"
        // ignore rule: empty css files do not matter
        property "sonar.issue.ignore.multicriteria.e3.ruleKey", "css:S4667"
        property "sonar.issue.ignore.multicriteria.e3.resourceKey", "**/*.scss"
    }
}
