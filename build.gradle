plugins {
    id "cloudflight-gradle-plugin" version "6.5.11"
}

// add version-postfix if build was triggered by customer
// no postfix if built within teamcity or with system property flag set to REMOVE
def customerPostfix = ""
if (!(System.getenv("TEAMCITY_BUILD") != null && System.getenv("TEAMCITY_BUILD").toBoolean())
        && (!System.getProperty("CUSTOMER_VERSION_POSTFIX") || System.getProperty("CUSTOMER_VERSION_POSTFIX") != "REMOVE")) {
    customerPostfix = System.getProperty("CUSTOMER_VERSION_POSTFIX") ? "-" + System.getProperty("CUSTOMER_VERSION_POSTFIX") : "-P"
}

description "Jems Project"
group "io.cloudflight.jems"
version "7.0.0$customerPostfix"

cloudflight {
    softwareProjectKey = "interact/ems"
}

subprojects {
    if (isKotlinProject(it)) {

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
                freeCompilerArgs += "-Xjvm-default=all"
            }
        }

        dependencies {
            implementation platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
            annotationProcessor platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
            testImplementation platform("io.cloudflight.platform:platform-test-bom:$cloudflightPlatformVersion")
            kapt platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")

            constraints {
                implementation "com.github.doyaaaaaken:kotlin-csv-jvm:0.15.0"
                implementation "org.mapstruct:mapstruct:1.4.2.Final"
                kapt 'org.mapstruct:mapstruct-processor:1.4.2.Final'
            }
        }
    }
    if (isServerProject(it) && (isCloudflightBuild(it) || System.getProperty("CUSTOMER_VERSION_POSTFIX"))) {
        bootJar {
            manifest {
                attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
            }
        }
    }
}

allprojects {
    configurations.all { exclude group: 'junit' }
}
