apply plugin: 'kotlin-jpa'

configurations {
    toDownload
}

if (isCloudflightBuild()) {
    bootJar {
        manifest {
            attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
        }
    }
}


dependencies {
    implementation('io.cloudflight.jems.plugin.contract:jems-plugin-contract')
    toDownload('io.cloudflight.jems.plugin.standard:jems-standard-plugin')

    implementation project(':jems-api')
    implementation project(':jems-ui')

    implementation('io.cloudflight.platform:platform-i18n')
    // platform-server-config is used for enabling built-in spring-actuators and to extract version details during build
    implementation('io.cloudflight.platform:platform-server-config')

    runtimeOnly('io.cloudflight.platform:platform-json')
    runtimeOnly('org.flywaydb:flyway-core')

    implementation('org.springframework.boot:spring-boot-actuator')
    implementation('org.springframework.boot:spring-boot-devtools')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('io.cloudflight.platform:platform-jpa')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('org.mariadb.jdbc:mariadb-java-client')
    implementation('org.elasticsearch.client:elasticsearch-rest-high-level-client')
    implementation('commons-io:commons-io')
    implementation('io.minio:minio')
    implementation('com.github.doyaaaaaken:kotlin-csv-jvm')

    implementation('io.springfox:springfox-swagger-ui')
    implementation('io.springfox:springfox-swagger2')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testImplementation('com.h2database:h2')
    testImplementation('io.cloudflight.platform:platform-test-jpa')

    constraints {
        implementation('commons-io:commons-io:2.8.0')
        implementation('io.minio:minio:7.0.2')
        implementation('org.mariadb.jdbc:mariadb-java-client:2.7.2')
        implementation('io.springfox:springfox-swagger-ui:2.9.2')
        implementation('io.springfox:springfox-swagger2:2.9.2')
        implementation('org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.2')
        implementation("io.cloudflight.jems.plugin.contract:jems-plugin-contract:$jemsPluginContractVersion")
        toDownload("io.cloudflight.jems.plugin.standard:jems-standard-plugin:$jemsStandardPluginVersion")
    }
}

task download(type: Copy) {
    from configurations.toDownload
    into 'build/libs/'
    include "jems-standard-plugin*.jar"
}

build.dependsOn download
